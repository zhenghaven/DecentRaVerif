(***************************** library Expansions ******************************)

	(***************************** CV_Common_Declarations ******************************)

expand CV_Common_Declarations(bitstringbot, bottom, emptyStr, keyseed, cleartext, ciphertext).

	(***************************** CV_DSA ******************************)

expand CV_det_signature_types(spkey, sskey, signinput, signature).
expand CV_det_signature_args(keyseed, spkey, sskey, signinput, signature, sskgen, spkgen, sign, checksign, getmess, getkey).

	(***************************** CV_RSA ******************************)

expand CV_public_key_enc_types(pkey, skey, aenc_seed).
expand CV_public_key_enc_args(keyseed, bitstringbot, bottom, pkey, skey, cleartext, ciphertext, aenc_seed, skgen, pkgen, aenc, aenc_r, adec, ainjbot, aZ).

	(***************************** CV_AEAD ******************************)

expand CV_keygen_types(key).
expand CV_keygen_args(keyseed, key, kgen).

expand CV_AEAD_types(key, add_data, nonce, zero_nonce, dummy_key, IncrNonce).
expand CV_AEAD_args(key, bitstringbot, bottom, bitstring, ciphertext, bitstring, nonce, enc, enc, dec, sinjbot, sZ, dummy_key).

	(***************************** DecentRA_EnclaveBas ******************************)

expand Decent_Enclave_Basics(spkey, enclaveProg, enclaveHash, HashEnclave, SPKey2Bitstr, EventSubType).

expand Decent_ECDH_Key(key, ecskey, ecpkey, ecpkgen, signinput, bitstring, ciphertext, bitstring, nonce, enc,
	DiHeValG, DiHeExp, EcDiHeToKey, EcDiHeKeyGen, EcPKeyToSignin,
	dummy_ecpkey, GetEcPKeyA, GetEcPKeyB).

	(***************************** DecentRA_LocAtt ******************************)

expand Decent_Local_Attestation(
	(** CV_ macros  **) keyseed, sskey, spkey, sign, signinput, signature,
	(** Encl Basics **) enclaveProg, enclaveHash, HashEnclave,
	(** ECHD Key    **) ecpkey,
	(** This. Rep   **) LocAttRep, GetLocAttSignedRep).

	(***************************** DecentRA_IasRep ******************************)

expand Decent_IAS_Report(
	(** CV_ macros  **) keyseed, sskey, spkey, sskgen, spkgen, sign, getmess, checksign, signinput, signature,
	(** Encl Basics **) enclaveProg, enclaveHash, HashEnclave,
	(** This. Vars  **) iasRepKeySeed, encPrvsKeySeed,
	(** This. Quote **) EnclaveQuoteSignin, GetEnclaveSignedQuote_intr, GetEnclaveSignedQuote,
	(** This. Rep   **) IasRep2Signin, GetSignedIasRep_intr, GetSignedIasRep,
	(** This. Proc  **) IasIssueRep, IasProc_intr).

	(***************************** DecentRA_AuthList ******************************)

expand Decent_Auth_List(enclaveHash, AuthListItem, AuthListNewItem, AuthList, AuthListInsert, AuthListEmpty, AuthListIsIn).

	(***************************** DecentRA_DecentSvr ******************************)

expand Decent_Decent_Server(
	(** CV_ macros    **) keyseed, sskey, spkey, sskgen, spkgen, getmess, sign, checksign, signinput, signature, emptyStr, key, bitstring, ciphertext, nonce, enc, dec, sinjbot, bottom, zero_nonce, IncrNonce,
	(** Encl Basics   **) enclaveProg, enclaveHash, HashEnclave, SPKey2Bitstr,
	(** ECHD Key      **) ecskey, ecpkey, ecpkgen, EcDiHeKeyGen,
	(** LocAtt Rep    **) LocAttRep, GetLocAttSignedRep,
	(** Ias Rep       **) encPrvsKeySeed, GetEnclaveSignedQuote_intr, IasRep2Signin,
	(** Lists         **) AuthList, AuthListNewItem, AuthListIsIn,
	(** This. Vars    **) enclaveDecentSvr, decentSvrName,
	(** This. SvrCert **) DecentSvrSignin, GetDecentSvrCert, VrfyDecentSvrCert_intr,
	(** This. AppCert **) DecentAppSignin, GetDecentAppCert, VrfyDecentAppCertChain_intr,
	(** This. SvrProc **) DecentSvrIssueCert, DecentSvrGotIasRep, DecentSvrProc_intr, DecentSvrProcP2_intr).

	(***************************** DecentRA_DecentApp ******************************)

expand Decent_Decent_App(
	(** CV_ macros    **) keyseed, sskey, spkey, sskgen, spkgen, getmess, sign, checksign, signinput, signature, emptyStr, key, bitstring, ciphertext, nonce, enc, dec, sinjbot, bottom, zero_nonce, IncrNonce,
	(** Encl Basics   **) enclaveProg, enclaveHash, SPKey2Bitstr, EventSubType,
	(** ECHD Key      **) ecskey, ecpkey, ecpkgen, EcDiHeKeyGen, EcPKeyToSignin,
	(** LocAtt Rep    **) LocAttRep, GetLocAttSignedRep,
	(** Decent Svr    **) DecentAppSignin, VrfyDecentAppCertChain_intr,
	(** Lists         **) AuthList,
	(** This. AppProc **) DecentAppAccPeer, DecentAppGotCert, DecentAppGotMsg, DecentAppSvrProc_intr, DecentAppCltProc_intr, DecentAppSvrProcP2_intr, DecentAppCltProcP2_intr).

(*============================================================================*)
(******************************* For this process *****************************)

(* Settings:*)
set attacker = active.

(* Channels: *)
free svrIasACh : channel.
free appSvrACh : channel.
free appAB_Ch : channel.

(* Decent App Materials *)
const enclaveA : enclaveProg.
const enclaveAName : bitstring.

const enclaveB : enclaveProg.
const enclaveBName : bitstring.

(******************************* Secrecy assumptions *****************************)

not attacker(iasRepKeySeed).
not attacker(encPrvsKeySeed).
not attacker(new svrKeySeed).
not attacker(new repKeySeed).
not attacker(new enclaveKeySeed).

not attacker(sskgen(iasRepKeySeed)).
not attacker(sskgen(encPrvsKeySeed)).
not attacker(sskgen(new svrKeySeed)).
not attacker(sskgen(new repKeySeed)).
not attacker(sskgen(new enclaveKeySeed)).

(******************************* Queries *****************************)

(* Query 1: event DecentAppGotCert(enclaveA, HashEnclave(enclaveDecentSvr), enclaveSPKeySeed) is reachable. *)
query let enclaveSPKeySeed = new enclaveKeySeed in
	let auLs =
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveA), enclaveAName),
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveDecentSvr), decentSvrName), AuthListEmpty)) in
	event(DecentAppGotCert(enclaveA, HashEnclave(enclaveDecentSvr), enclaveSPKeySeed, auLs)).

(* Query 2: event DecentSvrIssueCert(enclaveDecentSvr, HashEnclave(enclaveA), repSPKeySeed, enclaveSPKey, auLs) is reachable. *)
query let enclaveSPKey = spkgen(new enclaveKeySeed) in
	let repSPKeySeed = new repKeySeed in
	let auLs =
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveA), enclaveAName),
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveDecentSvr), decentSvrName), AuthListEmpty)) in
	event(DecentSvrIssueCert(enclaveDecentSvr, HashEnclave(enclaveA), repSPKeySeed, enclaveSPKey, auLs)).

(* Query 3:  *)
query anyEnclave : enclaveProg, anyEncKeyseed : keyseed, anyAuLs : AuthList;
	let repSPKeySeed = new repKeySeed in
	let auLs =
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveA), enclaveAName),
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveDecentSvr), decentSvrName), AuthListEmpty)) in
	inj-event(DecentAppGotCert(anyEnclave, HashEnclave(enclaveDecentSvr), anyEncKeyseed, anyAuLs)) ==>
	inj-event(DecentSvrIssueCert(enclaveDecentSvr, HashEnclave(anyEnclave), repSPKeySeed, spkgen(anyEncKeyseed), anyAuLs)).

(* Query 4:  *)
query anyEnclave : enclaveProg, anyEncKeyseed : keyseed, anyAuLs : AuthList;
	let enclaveSPKeySeed = new enclaveKeySeed in
	let repSPKeySeed = new repKeySeed in
	let auLs =
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveA), enclaveAName),
	AuthListInsert(AuthListNewItem(HashEnclave(enclaveDecentSvr), decentSvrName), AuthListEmpty)) in
	event(DecentSvrIssueCert(enclaveDecentSvr, HashEnclave(anyEnclave), repSPKeySeed, spkgen(anyEncKeyseed), anyAuLs)) ==>
	(anyEnclave = enclaveA) && (anyEncKeyseed = enclaveSPKeySeed) && (anyAuLs = auLs).

process
	let authList0 = AuthListInsert(AuthListNewItem(HashEnclave(enclaveDecentSvr), decentSvrName), AuthListEmpty) in
	let pub_authListA = AuthListInsert(AuthListNewItem(HashEnclave(enclaveA), enclaveAName), authList0) in

	let pub_iasRepSPKey : spkey = spkgen(iasRepKeySeed) in
	let pub_encPrvsKey : spkey = spkgen(encPrvsKeySeed) in

	(* IAS Server A    *) (!IasProc_intr(svrIasACh, iasRepKeySeed, pub_encPrvsKey))
	|
	(! (* An enclave platform (incl. Decent Server & Decent App) *)
		(
			new svrKeySeed : keyseed;
			new repKeySeed : keyseed;
			new enclaveKeySeed : keyseed;
			(* Decent Svr A    *) (!DecentSvrProc_intr(enclaveDecentSvr, svrKeySeed, svrIasACh, appSvrACh, pub_iasRepSPKey, encPrvsKeySeed, repKeySeed))
			|
			(* Decent App A    *) (!DecentAppSvrProc_intr(enclaveA, appSvrACh, appAB_Ch, false,
				pub_iasRepSPKey, repKeySeed, enclaveKeySeed, decentSvrName, enclaveBName, pub_authListA))
		)
	)
